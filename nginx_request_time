#!/usr/bin/perl -w

=head1 NAME

nginx_request_time - Munin plugin to show statistics of nginx's request_time.

=head1 APPLICABLE SYSTEMS

nginx request_time statistics

=head1 CONFIGURATION

File::ReadBackwards, Statistics::Lite, Statistics::Descriptive and Text::LTSV are used and must be installed.

You can override the log file location.

  [nginx_request_time]
      env.logfile /var/log/nginx/access.log
      env.loglimit 1000

Nginx must be configured to get $request_time.
nginx_request_time expects LTSV log format.

Example format:
log_format  ltsv  "time:$time_local"
                  "\thost:$remote_addr"
                  "\tforwardedfor:$http_x_forwarded_for"
                  "\treq:$request"
                  "\tstatus:$status"
                  "\tsize:$body_bytes_sent"
                  "\treferer:$http_referer"
                  "\tua:$http_user_agent"
                  "\treqtime:$request_time"
                  "\tcache:$upstream_http_x_cache"
                  "\truntime:$upstream_http_x_runtime"
                  "\tvhost:$host";

By default the last 1000 log lines are read, you may want to change this.

=head1 MAGIC MARKERS

  #%# family=auto
  #%# capabilities=autoconf

=head1 AUTHOR

t-cyrill
https://github.com/t-cyrill/munin-nginx_request_time

takeshiyako2
https://github.com/takeshiyako2/munin-nginx_request_time

=head1 LICENSE

MIT

=cut

use strict;
use warnings;
use File::ReadBackwards;
use Statistics::Lite qw(max min mean median);
use Statistics::Descriptive;
use Text::LTSV;

my $line_counter = exists $ENV{'loglimit'} ? $ENV{'loglimit'} : 1000;
my $LOG = exists $ENV{'logfile'} ? $ENV{'logfile'}
                                 : "/var/log/nginx/access.log";

if (exists $ARGV[0] and $ARGV[0] eq "autoconf") {
    unless (-r $LOG) {
        print "$LOG not readable\n";
    } else {
        print "yes\n";
    }
    exit 0;
}

my $mode = $0;
$mode =~ s/.*\/nginx_request_time//;

if (exists $ARGV[0] and $ARGV[0] eq "config") {
        print <<'EOS';
graph_title NGINX response time
graph_args -l 0 --base 1000
graph_category nginx
graph_vlabel millseconds
EOS
    if ($mode eq "_max") {
        print <<'EOS';
max.label max
max.draw LINE2
max.min 0
EOS
    } else {
        print <<'EOS';
min.label min
min.draw LINE1
min.min 0
median.label median
median.draw LINE2
median.min 0
percentile90.label 90th percentile
percentile90.draw LINE2
percentile90.min 0
percentile95.label 95th percentile
percentile95.draw LINE2
percentile95.min 0
EOS
    }
    exit 0;
}

my @request_time_array = ();
my $log_handle = File::ReadBackwards->new($LOG) or die "$!";

for (my $i = 0; $i < $line_counter; $i++) {
  my $log_line = $log_handle->readline;
  if (defined($log_line)) {
    my $p = Text::LTSV->new;
    my $hash = $p->parse_line($log_line);
    my $time_s = $hash->{reqtime};
    if (!defined($time_s)) { next; }
    push @request_time_array, $time_s * 1000;
  } else {
    last;
  }
}
$log_handle->close;

my $stat = Statistics::Descriptive::Full->new();
$stat->add_data(@request_time_array);
my $percentile90 = $stat->percentile(90);
my $percentile95 = $stat->percentile(95);

my $min = min @request_time_array;
my $max = max @request_time_array;
my $median = median @request_time_array;

print "min.value $min\n";
print "max.value $max\n";
print "median.value $median\n";
print "percentile90.value $percentile90\n";
print "percentile95.value $percentile95\n";
